
/*
buildscript {
    repositories {
        jcenter()
    }
}
//*/

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'


def libGroup = 'eu.monniot.redis'
def libVersion = '1.4.1-SNAPSHOT'
def libArtifact = 'embedded-redis'

group = libGroup
version = libVersion

description = """Redis embedded server for Java integration testing"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'redis.clients', name: 'jedis', version: '2.8.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.18'

    testCompile(group: 'org.mockito', name: 'mockito-core', version: '1.10.19') {
        exclude(module: 'hamcrest-core')
    }
    testCompile(group: 'junit', name: 'junit', version: '4.12') {
        exclude(module: 'hamcrest-core')
    }

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.18'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.4'
    testCompile group: 'org.powermock', name: 'powermock-core', version: '1.6.4'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.4'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}


// Maven publication
publishing {
    publications {
        StandardLibPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId libGroup
            artifactId libArtifact
            version libVersion
        }
    }
}

// Publish on bintray configuration
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publish = true
    dryRun = true

    publications = ['StandardLibPublication']

    pkg {
        repo = 'maven'
        name = libArtifact
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/fmonniot/embedded-redis.git'
        publicDownloadNumbers = true

        version {
            name = libVersion
        }
    }
}